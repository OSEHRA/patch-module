A1AEPH5	;RMO/ALBANY : Detailed Report of Verified Patches by Date;22 OCT 87 11:00 am;24 NOV 87 11:00 am ;6/29/07  10:59
	;;2.4;PATCH MODULE;;Mar 28, 2014;Build 8
	;;Version 2.2;PROBLEM/PATCH REPORTING;;11/23/92;Build 2
	S %DT="",X="T" D ^%DT S DT=Y D PKGPMT^A1AEVP G Q:X["^"!($D(A1AENOSL)) D DATE^A1AEUTL1 G Q:'$D(BEGDATE)!('$D(ENDDATE)) S BEGDATE=9999999-BEGDATE,ENDDATE=9999999-ENDDATE
	S PGM="START^A1AEPH5",VAR=$S($D(A1AEPKIF):"A1AEPKIF^DUZ^BEGDATE^ENDDATE",1:"DUZ^BEGDATE^ENDDATE") W ! D ZIS^A1AEUTL1 G Q:POP
	;
START	U IO K A1AEPGE,A1AELNE,^UTILITY($J,"W") S A1AEHD="DHCP Detailed Report of Released Patches",$P(A1AELNE,"-",78)="",^UTILITY($J,1)="D HD^A1AEPH2",DIWF="B4X",A1AEOUT="" ;SO DI*22*152
	S Y=9999999-BEGDATE D D^DIQ S A1AEBEG=Y S Y=9999999-ENDDATE D D^DIQ S A1AEND=Y I $D(A1AEPKIF) D GETDTV G Q
	F A1AEPKIF=0:0 S A1AEPKIF=+$O(^A1AE(11007,"AU",DUZ,A1AEPKIF)) Q:'A1AEPKIF!(A1AEOUT["^")  I $D(^A1AE(11007,A1AEPKIF,0)) D GETDTV:$P(^(0),"^",2)="Y"!($P(^(0),"^",4)="y")
	;
Q	I '$D(A1AEPGE)&$D(A1AEBEG)&$D(A1AEND) W !?3,"There were no patches released ",$S(A1AEBEG=A1AEND:"on "_A1AEBEG,1:"between "_A1AEBEG_" and "_A1AEND),"."
	W ! K ^UTILITY($J),DN,DXS,A1AEPKIF,A1AEPKNM,A1AEDTV,A1AEHD,A1AEOUT,A1AEIFN,A1AEPGE,BEGDATE,ENDDATE,A1AEBEG,A1AEND,VAR,PGM,POP,A1AENOSL,A1AELNE,A1AEPK D CLOSE^A1AEUTL1 Q
	;
GETDTV	F A1AEDTV=ENDDATE-1:0 S A1AEDTV=$O(^A1AE(11005,"AV",A1AEPKIF,A1AEDTV)) Q:'A1AEDTV!(BEGDATE<A1AEDTV)!(A1AEOUT["^")  D GETIFN
	Q
	;
GETIFN	F A1AEIFN=0:0 S A1AEIFN=$O(^A1AE(11005,"AV",A1AEPKIF,A1AEDTV,A1AEIFN)) Q:'A1AEIFN!(A1AEOUT["^")  I $D(^A1AE(11005,A1AEIFN,0)),$P(^(0),"^",8)="v" S D0=A1AEIFN K DXS D HD^A1AEPH2,^A1AEP K DN,DXS,^UTILITY($J,"W")
	Q
