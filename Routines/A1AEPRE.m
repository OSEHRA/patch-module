A1AEPRE	;RMO/ALBANY ; Pre-Initialization Procedure ;24 NOV 87 11:00 am
	;;2.4;PATCH MODULE;;Mar 28, 2014;Build 8
	;;Version 2.1 ;February 23, 1990
	S:'$D(^A1AE("VERSION")) ^A1AE("VERSION")=1.1 Q:^A1AE("VERSION")>1.1  S IOP="HOME" D ^%ZIS K IOP S %DT="",X="T" D ^%DT S DT=Y
	W !!,"...forcing 'Completed/not Verified' Patches to 'Verified' by POSTMASTER"
	F A1AEPKIF=0:0 S A1AEPKIF=$O(^A1AE(11005,"AV",A1AEPKIF)) Q:'A1AEPKIF  D GETDTC
	W !,"...removing the 'AV' cross-reference from DATE PATCH COMPLETED field"
	F A1AEXREF=0:0 S A1AEXREF=$O(^DD(11005,10,1,A1AEXREF)) Q:'A1AEXREF  I $D(^(A1AEXREF,0)),$P(^(0),"^",2)="AV" S DA(2)=11005,DA(1)=10,DA=A1AEXREF,DIK="^DD(11005,10,1," D ^DIK K DA,DIK
	;
Q	K ^DOPT("A1AEPH2"),A1AE0,A1AEDTC,A1AEIFN,A1AEPKIF,A1AEXREF
	Q
	;
GETDTC	F A1AEDTC=0:0 S A1AEDTC=$O(^A1AE(11005,"AV",A1AEPKIF,A1AEDTC)) Q:'A1AEDTC  D GETIFN
	Q
	;
GETIFN	F A1AEIFN=0:0 S A1AEIFN=$O(^A1AE(11005,"AV",A1AEPKIF,A1AEDTC,A1AEIFN)) Q:'A1AEIFN  I $D(^A1AE(11005,A1AEIFN,0)),$P(^(0),"^",8)="c" S A1AE0=^(0) D SETVER W "."
	Q
	;
SETVER	S $P(^A1AE(11005,A1AEIFN,0),"^",8)="v",$P(^(0),"^",11)=DT,$P(^(0),"^",14)=.5
	K ^A1AE(11005,"AS",A1AEPKIF,+$P(A1AE0,"^",3),"c",+$P(A1AE0,"^",4),A1AEIFN) S ^A1AE(11005,"AS",A1AEPKIF,+$P(A1AE0,"^",3),"v",+$P(A1AE0,"^",4),A1AEIFN)=""
	K ^A1AE(11005,"AV",A1AEPKIF,A1AEDTC,A1AEIFN) S ^A1AE(11005,"AV",A1AEPKIF,9999999-DT,A1AEIFN)=""
	Q
